[ entry point ]
dragon.main()() = {
    [ startup message ]
    dragon.set("Prickling everything in the desert... Ouch!%0a;")(start_up_message)
    dragon.print.buffer_as_string(start_up_message)()

    [ run tests ]
    cactus.run_test.basic()(error)
    dragon.error.print_if_occured(error)()
    cactus.run_test.aliases()(error)
    dragon.error.print_if_occured(error)()
    cactus.run_test.structures()(error)
    dragon.error.print_if_occured(error)()
}

[ run test ]
cactus.run_test.basic()(compilation.error !dragon.error) = {
    [ get json files ]
    dragon.set("../cactus/source/files.json")(cactus_compiler.json.file_path)
    dragon.set("./tests/basic/files.json")(cactus_testing.json.file_path)

    [ run test ]
    cactus.run_test(cactus_compiler.json.file_path, cactus_testing.json.file_path)(compilation.error, program.result, program.error)
}

[ run test alias ]
cactus.run_test.aliases()(compilation.error !dragon.error) = {
    [ get json files ]
    dragon.set("../cactus/source/files.json")(cactus_compiler.json.file_path)
    dragon.set("./tests/aliases/files.json")(cactus_testing.json.file_path)

    [ run test ]
    cactus.run_test(cactus_compiler.json.file_path, cactus_testing.json.file_path)(compilation.error, program.result, program.error)
}

[ run test functions ]
cactus.run_test.structures()(compilation.error !dragon.error) = {
    [ get json files ]
    dragon.set("../cactus/source/files.json")(cactus_compiler.json.file_path)
    dragon.set("./tests/structures/files.json")(cactus_testing.json.file_path)

    [ run test ]
    cactus.run_test(cactus_compiler.json.file_path, cactus_testing.json.file_path)(compilation.error, program.result, program.error)
}

[ gather all files and run a test ]
cactus.run_test(cactus_compiler.json_file_path !dragon.buffer, cactus_testing.json_file_path !dragon.buffer)(compilation.error !dragon.error, program.result !dragon.buffer, program.error !dragon.buffer) = {
    [ get cactus compiler files ]
    cactus.gather_files(cactus_compiler.json_file_path)(cactus.compiler.files, success, failure)
    dragon.jump.bottom(failure, @dragon.master_scope)()

    [ get cactus test files ]
    cactus.gather_files(cactus_testing.json_file_path)(cactus.testing.files.list, success, failure)
    dragon.jump.bottom(failure, @dragon.master_scope)()
    dragon.list.duplicate_content(cactus.testing.files.list)(cactus.testing.files.buffer)
    dragon.list.close(cactus.testing.files.list)()

    [ DEBUG ]
    [ cactus.print.files_buffer(cactus.testing.files.buffer)() ]

    [ run test ]
    dragon.just_run(cactus.compiler.files, cactus.testing.files.buffer)(compilation.error, program.result, program.error)
    dragon.error.print_if_occured(compilation.error)()

    [ close data ]
    dragon.list.close(cactus.compiler.files)()
    dragon.buffer.return(cactus.testing.files.buffer)()
}

[ get files ]
cactus.gather_files(json_file_path !dragon.buffer)(files !dragon.list, success !dragon.cell, failure !dragon.cell) = {
    [ open json file ]
    dragon.file_to_buffer(json_file_path)(json_buffer)
    dragon.integer.within_range(dragon.constant.0, json_buffer:start, dragon.constant.0)(is_empty, is_not_empty)
    @file_path_error is_empty = {
        [ print error message ]
        dragon.set("Cactus Error: Starting JSON file was not found.")(message)
        dragon.print.buffer_as_string(message)()
        dragon.print.new_line()()

        [ set status ]
        dragon.copy(dragon.false)(success)
        dragon.copy(dragon.true)(failure)

        [ exit function ]
        dragon.jump.bottom(dragon.always, @dragon.master_scope)()
    }

    [ compile json ]
    dragon.json.compile(json_buffer, dragon.false)(json_workspace)
    @json_error json_workspace:error:occured = {
        [ print error message ]
        dragon.set("Cactus Error: Invalid json file.")(message)
        dragon.print.buffer_as_string(message)()
        dragon.print.new_line()()

        [ clean up ]
        dragon.json.compile.close.workspace(json_workspace)()
        dragon.buffer.return(json_buffer)()

        [ set status ]
        dragon.copy(dragon.false)(success)
        dragon.copy(dragon.true)(failure)

        [ exit function ]
        dragon.jump.bottom(dragon.always, @dragon.master_scope)()
    }

    [ pack files into list ]
    [ open list ]
    dragon.set(dragon.integer.1024)(increase)
    dragon.structure.byte_size(json_buffer)(buffer.byte_size)
    dragon.integer.multiply(increase, buffer.byte_size)(increase)
    dragon.list.open(increase)(files_list)

    [ get files keyword ]
    dragon.set("file_paths")(keyword)
    dragon.json.traverse.retrieve_parsling.by_name(json_workspace:parslings:root, keyword)(json_paths_parsling, found, not_found)
    dragon.json.traverse.calculate.parsling_list_metadata(json_paths_parsling)(content, parsling.byte_size, parsling_count)

    [ setup math ]
    dragon.integer.subtract(parsling_count, dragon.constant.1)(max_parsling_index)
    dragon.copy(dragon.constant.0)(current_parsling_index)

    [ append files to list ]
    @get_files.loop dragon.always = {
        [ check for loop end ]
        dragon.integer.within_range(dragon.constant.0, current_parsling_index, max_parsling_index)(in_range, out_of_range)
        dragon.jump.bottom(out_of_range, @get_files.loop)()

        [ get file path ]
        dragon.json.traverse.retrieve_parsling.by_index(json_paths_parsling, current_parsling_index)(found_parsling, valid_index, invalid_index)

        [ trim double quotes off of file path ]
        cactus.string.trim_double_quotes(found_parsling:value.datum:raw)(trimmed_file_path)

        [ load file ]
        dragon.file_to_buffer(trimmed_file_path)(file)
        dragon.integer.within_range(dragon.constant.0, file:start, dragon.constant.0)(not_loaded, loaded)
        @get_files.error not_loaded = {
            [ print error message ]
            dragon.set("Cactus Error: Invalid file path inside json file.")(message)
            dragon.print.buffer_as_string(trimmed_file_path)()
            dragon.print.buffer_as_string(message)()
            dragon.print.new_line()()

            [ clean up ]
            dragon.json.compile.close.workspace(json_workspace)()
            dragon.buffer.return(json_buffer)()
            dragon.list.close(files_list)()

            [ set status ]
            dragon.copy(dragon.false)(success)
            dragon.copy(dragon.true)(failure)

            [ exit function ]
            dragon.jump.bottom(dragon.always, @dragon.master_scope)()
        }

        [ append file ]
        dragon.list.append.buffer(files_list, file)(files_list)

        [ next file index ]
        dragon.integer.add(current_parsling_index, dragon.constant.1)(current_parsling_index)

        [ jump to top of loop ]
        dragon.jump.top(dragon.always, @get_files.loop)()
    }

    [ change from list to buffer ]
    [ dragon.list.duplicate_content(files_list)(files) ]

    [ deallocate old list ]
    [ dragon.list.close(files_list)() ]

    [ copy files ]
    dragon.copy(files_list)(files)

    [ set status ]
    dragon.copy(dragon.true)(success)
    dragon.copy(dragon.false)(failure)
}

[ trim double quotes off of string ]
cactus.string.trim_double_quotes(input !dragon.buffer)(output !dragon.buffer) = {
    [ trim ]
    dragon.integer.add(input:start, dragon.constant.1)(output:start)
    dragon.integer.subtract(input:end, dragon.constant.1)(output:end)
}

[ print files in buffer ]
cactus.print.files_buffer(files !dragon.buffer)() = {
    [ create current ]
    dragon.pack(files, files:start)(current !dragon.current)

    [ setup constants ]
    dragon.structure.byte_size(files)(buffer.byte_size)
    
    [ print loop ]
    @loop dragon.always = {
        [ check for more files ]
        dragon.current.within_range(current)(in_range, out_of_range)
        dragon.jump.bottom(out_of_range, @dragon.master_scope)()

        [ get buffer ]
        dragon.buffer.calculate.end_address(current:progress, buffer.byte_size)(buffer.end)
        dragon.pack(current:progress, buffer.end)(file.buffer !dragon.buffer)
        dragon.buffer_to_structure(file.buffer)(file !dragon.buffer)

        [ print file ]
        dragon.print.buffer_as_string(file)()

        [ advance current ]
        dragon.integer.add(current:progress, buffer.byte_size)(current:progress)

        [ jump to top of loop ]
        dragon.jump.top(dragon.always, @loop)()
    }
}
